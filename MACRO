REM  *****  BASIC  *****

Sub ConvertWordToPDF(cFile)
   cURL = ConvertToURL(cFile)
   
   ' Open the document.
   ' Just blindly assume that the document is of a type that OOo will
   '  correctly recognize and open -- without specifying an import filter.
   oDoc = StarDesktop.loadComponentFromURL(cURL, "_blank", 0, Array(MakePropertyValue("Hidden", True), ))

   Dim comps
   comps = split (cFile, ".")
   If UBound(comps) > 0 Then
       comps(UBound(comps)) = "pdf"
       cfile = join (comps, ".")
   Else
       cfile = cFile + ".pdf"
   Endif

   cURL = ConvertToURL(cFile)
   
   ' Save the document using a filter.
   oDoc.storeToURL(cURL, Array(MakePropertyValue("FilterName", "writer_pdf_Export"), ))
   
   oDoc.close(True)
   
End Sub

Function MakePropertyValue( Optional cName As String, Optional uValue ) As com.sun.star.beans.PropertyValue
   Dim oPropertyValue As New com.sun.star.beans.PropertyValue
   If Not IsMissing( cName ) Then
      oPropertyValue.Name = cName
   EndIf
   If Not IsMissing( uValue ) Then
      oPropertyValue.Value = uValue
   EndIf
   MakePropertyValue() = oPropertyValue
End Function